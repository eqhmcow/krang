#!/usr/bin/env perl 
use warnings;
use strict;

=pod

=head1 NAME

krang_apachectl

=head1 SYNOPSIS

   krang_apachectl start

   krang_apachectl stop

   krang_apachectl restart

   krang_apachectl offline

=head1 DESCRIPTION

This script controls the Krang Apache server. The usage of the script
is the same as the normal apache "apachectl" script but only "start",
"stop" and "restart" are supported. Also, "restart" performs a hard
stop and start since a SIGHUP isn't enough for mod_perl. We've also added
a command "offline" that stops the normal Krang Apache and restarts one
to show an offline page.

Usage information can be obtained by running the script with no arguments.

=head1 OPTIONS

=over

=item --cgi-mode

If true, will cause all CGI applications to be run as true
CGIs, via mod_cgi. Otherwise all Perl files will be run under
mod_perl using L<Apache::Registry>.

CGI mode is only useful for debugging during development. In production
is it inadvisable to add this option.
This option can also be turned on using the C<CGI_MODE> environment variable.

=item --profile-mode

If true, then Krang will use L<Devel::NYTProf> and run with a single
process so that the code can be profiled. L<Devel::NYTProf> will
print a banner indicating where the profiling data is stored and it can
be analyzed with programs like C<nytprofhtml>.

This option can also be turned on using the C<PROFILE_MODE> environment variable.

=back

=cut

# how long to wait for stop on restart (in seconds)
use constant MAX_RESTART_WAIT => 20;

# location of ApacheRoot, relative to $KRANG_ROOT
use constant APACHE_ROOT => 'apache/';

# location of ApacheBin, relative to ApacheRoot
use constant APACHE_BIN => 'bin/httpd';

$|++;

use File::Spec::Functions qw(catdir catfile splitdir rel2abs canonpath);
use FindBin qw($RealBin);
use Config;

BEGIN {

    # Find a KRANG_ROOT based on path to bin
    unless ($ENV{KRANG_ROOT}) {
        my @dir = splitdir(canonpath($RealBin));
        $ENV{KRANG_ROOT} ||= catdir(@dir[0 .. $#dir - 1]);
    }

    # use $KRANG_ROOT/lib for modules
    my $lib = catdir($ENV{KRANG_ROOT}, "lib");
    $ENV{PERL5LIB} =
      $ENV{PERL5LIB}
      ? "$ENV{PERL5LIB}:${lib}"
      : "${lib}";
    unshift @INC, $lib, "$lib/" . $Config{archname};

    # load Krang::Conf (don't need to load Krang here because we won't
    # be slinging elements)
    eval { require Krang::Conf };
    warn << "END" and exit(1) if $@;
######################################################################

Cannot load Krang.

Error message:

$@

######################################################################
END
}

use Krang::ClassFactory qw(pkg);
use Krang::ClassLoader Conf => qw(KrangRoot InstanceHostName Skin KrangUser KrangGroup EnableSSL EnablePreviewEditor
  InstanceApacheAddr InstanceApachePort InstanceSSLCertificateFile
  InstanceSSLCertificateKeyFile InstanceSSLCertificateChainFile
  InstanceSSLCACertificateFile InstanceSSLCARevocationFile InstanceSSLPort
  RewriteLogLevel AvailableLanguages BrowserSpeedBoost
);
use Krang::ClassLoader 'File';
use Krang::ClassLoader Localization => qw(%LANG);
use Krang::ClassLoader 'IO';
use Getopt::Long;
use Capture::Tiny qw(capture);

my $cgi_mode     = exists $ENV{CGI_MODE}     && $ENV{CGI_MODE}     ? 1 : 0;
my $profile_mode = exists $ENV{PROFILE_MODE} && $ENV{PROFILE_MODE} ? 1 : 0;
GetOptions(
    'cgi|cgi-mode|cgi_mode'             => \$cgi_mode,
    'profile|profile-mode|profile_mode' => \$profile_mode,
);

print STDERR "CGI Mode activated\n" if $cgi_mode;
print STDERR "Profile Mode (using Devel::NYTProf) activated\n" if $profile_mode;

# SSL directives
my @ssl_directives = qw(
  SSLApachePort
  SSLPassPhraseDialog
  SSLRandomSeedStartup
  SSLRandomSeedConnect
  SSLSessionCacheTimeout
  SSLProtocol
  SSLCipherSuite
  SSLVerifyClient
  SSLVerifyDepth
  SSLLogLevel
);

# the httpd command
my $apache_bin = rel2abs(catfile(KrangRoot, APACHE_ROOT, APACHE_BIN));
my @httpd = ($apache_bin, '-f', rel2abs(catfile(KrangRoot, "tmp", "httpd.conf")));

# get status from the pid file
my $pid_file         = catfile(KrangRoot, 'tmp', 'httpd.pid');
my $offline_pid_file = catfile(KrangRoot, 'tmp', 'httpd_offline.pid');
my $running         = is_pid_running($pid_file);
my $offline_running = is_pid_running($offline_pid_file);

# make language string for messages
my %languages = ();
@languages{AvailableLanguages, 'en'} = ();
my $langs = join q{,}, map { "'$LANG{$_}'" } sort keys %languages;

# check for args
usage() unless @ARGV;

my $command = shift @ARGV;

if ($command eq 'start') {
    die "krang_apachectl $command: httpd (pid $running) already running\n" if $running;
    die "krang_apachectl $command: offline httpd (pid $offline_running) already running\n"
      if $offline_running;

    # start
    start();
}

if ($command eq 'stop') {
    unless ($running || $offline_running) {
        die "krang_apachectl $command: httpd not running\n";
    }

    if( $running ) {
        if (kill 15, $running) {
            print "krang_apachectl $command: httpd stopped\n";
            exit;
        } else {
            die "krang_apachectl $command: httpd could not be stopped\n";
        }
    }
    if( $offline_running ) {
        if (kill 15, $offline_running) {
            print "krang_apachectl $command: httpd stopped\n";
            exit;
        } else {
            die "krang_apachectl $command: httpd could not be stopped\n";
        }
    }
}

if ($command eq 'restart') {
    stop_pid($running,         $pid_file,         'httpd')         if $running;
    stop_pid($offline_running, $offline_pid_file, 'offline httpd') if $offline_running;
    start();
}

if ($command eq 'offline') {
    stop_pid($running,         $pid_file,         'httpd')         if $running;
    stop_pid($offline_running, $offline_pid_file, 'offline httpd') if $offline_running;
    offline();
}

usage();

sub usage {
    print <<END;
usage: krang_apachectl (start|stop|restart|debug)

start      - start httpd
stop       - stop httpd
restart    - restart httpd if running

END

    exit;
}

sub start {
    setup_logs();
    write_conf();
    make_links();
    apply_skin();
    make_templates();
    make_js_localizer();
    prepare_for_browser_speed_boost();
    make_js_for_poortext();
    combine_preview_editor_js() if EnablePreviewEditor;

    print "krang_apachectl $command: starting httpd\n";
    my $return;
    my ($out, $err) = capture { $return = system(@httpd) };
    print $out;
    print STDERR $err;
    if($return == 0) {
        print "krang_apachectl $command: httpd started";
        print " (SSL enabled)" if EnableSSL;
        print "\n";
        exit;
    } else {
        die "krang_apachectl $command: httpd could not be started\n";
    }
}

sub write_conf {
    require HTML::Template;

    # find template file, looking addons first then in the root
    my $file =
      pkg('File')
      ->find($command eq 'offline' ? "conf/httpd_offline.conf.tmpl" : "conf/httpd.conf.tmpl");

    # create the httpd.conf by processing it as a template, using conf
    # directives as params
    my $template = HTML::Template->new(
        filename          => $file,
        global_vars       => 1,
        die_on_bad_params => 0,
    );

    my $apache_root = rel2abs(catdir(KrangRoot, APACHE_ROOT));
    $template->param(ApacheRoot => $apache_root);

    # CGI/Profile modes
    $template->param(
        CGI_MODE     => $cgi_mode,
        PROFILE_MODE => $profile_mode,
    );

    $template->param(
        (
            map { ($_, scalar pkg('Conf')->get($_)) } (
                qw(KrangUser KrangGroup
                  ApacheAddr ApachePort
                  HostName
                  RewriteLogLevel
                  MinSpareServers
                  MaxSpareServers
                  MaxClients
                  )
            )
        ),
        KrangRoot => rel2abs(pkg('Conf')->get("KrangRoot"))
    );

    # SSL
    if (pkg('Conf')->EnableSSL) {
        my %tmpl_params = (EnableSSL => 1);
        foreach my $dir (@ssl_directives) {
            $tmpl_params{$dir} = pkg('Conf')->get($dir) || undef;
        }

        my $verify_client = pkg('Conf')->SSLVerifyClient;

        # we need to verify the client cert unless it's not set or set to 'none'
        if ($verify_client && $verify_client ne 'none') {
            $tmpl_params{SSL_VERIFY_CLIENT} = 1;
        }
        $template->param(%tmpl_params);
    }

    # setup the instance loop
    my @loop;
    foreach my $instance (pkg('Conf')->instances()) {
        pkg('Conf')->instance($instance);
        push(
            @loop,
            {
                InstanceName                    => $instance,
                InstanceHostName                => InstanceHostName(),
                InstanceApacheAddr              => scalar InstanceApacheAddr(),
                InstanceApachePort              => scalar InstanceApachePort(),
                InstanceSSLCertificateFile      => scalar InstanceSSLCertificateFile(),
                InstanceSSLCertificateKeyFile   => scalar InstanceSSLCertificateKeyFile(),
                InstanceSSLCertificateChainFile => scalar InstanceSSLCertificateChainFile(),
                InstanceSSLCACertificateFile    => scalar InstanceSSLCACertificateFile(),
                InstanceSSLCARevocationFile     => scalar InstanceSSLCARevocationFile(),
                InstanceSSLPort                 => scalar InstanceSSLPort(),
                RewriteLogLevel                 => scalar RewriteLogLevel(),
            }
        );
    }
    $template->param(InstanceLoop => \@loop);

    # write out the config
    open(CONF, '>' . catfile(KrangRoot, "tmp", "httpd.conf"))
      or die "Unable to open " . catfile(KrangRoot, "tmp", "httpd.conf") . ": $!";
    print CONF $template->output();
    close CONF;
}

# setup symlinks - remove the old ones first if they exist.
# (FIX: when Apache::Handler supports aliases this can go away)

sub make_links {

    my @links = (
        {
            src => catdir(KrangRoot, 'data'),
            dst => catfile(KrangRoot, 'htdocs', 'data')
        },
        {
            src => catdir(KrangRoot, 'tmp'),
            dst => catfile(KrangRoot, 'htdocs', 'tmp'),
        },
    );

    do_make_links(@links);
}

sub do_make_links {
    my @links = @_;

    for my $link (@links) {

        unlink $link->{dst} if -l $link->{dst};

        if (-e $link->{src}) {
            system("ln -s $link->{src} $link->{dst}")
              and die "Couldn't symlink $link->{src} to $link->{dst}: $!";
        }
    }
}

# setup the configured skin
sub apply_skin {
    pkg('IO')->say("Loading configured skin: " . Skin);
    system(catfile(KrangRoot, 'bin', 'krang_load_skin'), Skin);
}

# make sure logs get created with the right ownership when running as root
sub setup_logs {

    # only worry if we're root
    return if $< != 0 or $> != 0;

    foreach my $log (qw(apache_access_log apache_error_log krang.log)) {
        my $file = catfile(KrangRoot, 'logs', $log);
        next if -e $file;

        # create the file and chown it to KrangUser:KrangGroup
        system("touch", $file);
        system("chown", KrangUser . ":" . KrangGroup, $file);
    }
}

sub make_templates {
    pkg('IO')->say("Making localized templates for $langs.");
    system(catfile(KrangRoot, 'lang', 'bin', 'krang_localize_templates'))
      and die "Couldn't localize templates: $?";
}

sub make_js_localizer {
    pkg('IO')->say("Making JavaScript localizer for $langs.");
    system(catfile(KrangRoot, 'lang', 'bin', 'krang_make_js_loc'))
      and die "Couldn't make JavaScript localizer: $?";
}

sub prepare_for_browser_speed_boost {

    # only run if we have an instance set up with BrowserSpeedBoost
    my $go_ahead      = 0;
    my $orig_instance = pkg('Conf')->instance();
    foreach my $instance (pkg('Conf')->instances) {
        pkg('Conf')->instance($instance);
        if (BrowserSpeedBoost) {
            $go_ahead = 1;
            last;
        }
    }

    return unless $go_ahead;

    pkg('IO')->say("Making Combined JavaScript files for $langs.");
    system(catfile(KrangRoot, 'bin', 'krang_combine_js'))
      and die "Couldn't make JavaScript combined files: $?";

    print "Clearing out old minified and compressed asset files.\n";
    system(catfile(KrangRoot, 'bin', 'krang_clear_minified_assets'))
      and die "Couldn't clear out minified assets: $?";
}

sub make_js_for_poortext {
    system(catfile(KrangRoot, 'htdocs', 'poortext', 'bin', 'krang_make_poortext'), '--verbose')
      and warn 
      "Couldn't make browser engine-specific JavaScript for Krang::ElementClass::PoorText: $?";
}

sub is_pid_running {
    my $pid_file = shift;
    my $running  = 0;
    my $pid;
    if (-e $pid_file) {
        $pid = `cat $pid_file`;
        chomp $pid;
        if ($pid and kill(0, $pid)) {
            $running = 1;
        }
    }
    return $running ? $pid : undef;
}

sub stop_pid {
    my ($pid, $pid_file, $which) = @_;
    if (kill 15, $pid) {
        print "krang_apachectl $command: waiting for $which to stop";
    } else {
        die "krang_apachectl $command: $which could not be stopped\n";
    }

    # wait for stop - check pid file for removal
    my $stopped = 0;
    for my $wait (0 .. MAX_RESTART_WAIT) {
        if (not -e $pid_file) {
            $stopped = 1;
            last;
        }
        print ".";
        sleep 1;
    }
    if ($stopped) {
        print "\nkrang_apachectl $command: $which stopped\n";
    } else {
        die "\nkrang_apachectl $command: $which not stopped in time\n";
    }
}

sub make_offline_pages {
    require HTML::Template;

    # find template file, looking addons first then in the root
    my $file = pkg('File')->find("templates/offline.tmpl");

    foreach my $instance (pkg('Conf')->instances) {
        pkg('Conf')->instance($instance);
        my $template = HTML::Template->new(
            filename          => $file,
            global_vars       => 1,
            die_on_bad_params => 0,
        );
        my %params = (
            InstanceName        => $instance,
            InstanceDisplayName => scalar pkg('Conf')->get('InstanceDisplayName'),
            CustomCSS           => scalar pkg('Conf')->get('CustomCSS'),
        );
        $template->param(%params);
        my $out_file = catfile(KrangRoot, 'htdocs', 'offline', "$instance.html");
        open(my $OUT, '>', $out_file) or die "Could not open file $out_file for output: $!";
        print $OUT $template->output;
        close($OUT);
    }
}

sub offline {
    setup_logs();
    write_conf();
    apply_skin();
    make_offline_pages();

    print "krang_apachectl offline: starting httpd\n";
    my $return;
    my ($out, $err) = capture { $return = system(@httpd) };
    print $out;
    print STDERR $err;
    if($return == 0 ) {
        print "krang_apachectl offline: httpd started";
        print " (SSL enabled)" if EnableSSL;
        print "\n";
        exit;
    } else {
        die "krang_apachectl offline: httpd could not be started\n";
    }
}

sub combine_preview_editor_js {
    pkg('IO')->say("Making Combined JavaScript file for Preview Editor.");
    system(catfile(KrangRoot, 'htdocs', 'preview_editor', 'bin', 'krang_combine_preview_editor_js') . ' --minify')
      and die "Couldn't make JavaScript combined file: $?";
}
