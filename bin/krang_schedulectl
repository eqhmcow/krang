#!/usr/bin/perl
use warnings;
use strict;

=pod

=head1 NAME

krang_schedulectl

=head1 SYNOPSIS

   krang_schedulectl start

   krang_schedulectl stop

   krang_schedulectl restart

=head1 DESCRIPTION

This script controls the Krang Schedule Daemon.  Instead of calling this
script directly, call krang_ctl instead.

=cut

# how long to wait for stop on restart (in seconds)
use constant MAX_RESTART_WAIT => 10;
$|++;

use File::Spec::Functions qw(catdir catfile splitdir rel2abs canonpath);
use FindBin qw($RealBin);
use Config;

BEGIN {

    # Find a KRANG_ROOT based on path to bin
    my @dir = splitdir(canonpath($RealBin));
    $ENV{KRANG_ROOT} ||= catdir(@dir[0 .. $#dir - 1]);

    # use $KRANG_ROOT/lib for modules
    my $lib = catdir($ENV{KRANG_ROOT}, "lib");
    $ENV{PERL5LIB} = $ENV{PERL5LIB} ? "$ENV{PERL5LIB}:${lib}" : "${lib}";
    unshift @INC, $lib, "$lib/" . $Config{archname};

    # load Krang::Conf (don't need to load Krang here because we won't
    # be slinging elements)
    eval { require Krang::Conf };
    warn << "END" and exit(1) if $@;
######################################################################

Cannot load Krang.

Error message:

$@

######################################################################
END
}

use Krang::ClassFactory qw(pkg);
use Krang::ClassLoader Conf => qw(KrangRoot KrangUser KrangGroup SchedulerMaxChildren);

# the krang_scheduled command
my $scheduled = rel2abs(catfile(KrangRoot, 'bin', 'krang_scheduled'));

# get status from the pid file
my $pid_file = catfile(KrangRoot, "tmp", "schedule_daemon.pid");
my $running = 0;
my $pid;
if (-e $pid_file) {
    $pid = `cat $pid_file`;
    chomp $pid;
    if ($pid and kill(0, $pid)) {
        $running = 1;
    } elsif ($pid) {
        unlink $pid_file;
    }
}

# check for args
usage() unless @ARGV;

my $command = shift @ARGV;

if ($command eq 'start') {

    unless (SchedulerMaxChildren > 0) {
        print "krang_schedulectl $command: skipping -- SchedulerMaxChildren set to 0.\n";
        exit;
    }

    if ($running) {
        die "krang_schedulectl $command: krang_scheduled (pid $pid) already " . "running\n";
    }

    print "krang_schedulectl $command: starting krang_scheduled\n";

    setup_logs();

    unless (system($scheduled)) {
        print "krang_schedulectl $command: krang_scheduled started\n";
        exit;
    } else {
        die "krang_schedulectl $command: krang_scheduled could not be " . "started\n";
    }
}

if ($command eq 'stop') {
    unless ($running) {
        die "krang_schedulectl $command: krang_scheduled not running\n";
    }
    if (kill 15, $pid) {
        print "krang_schedulectl $command: krang_scheduled stopped\n";
        exit;
    } else {
        die "krang_schedulectl $command: krang_scheduled could not be " . "stopped\n";
    }
}

if ($command eq 'restart') {

    # stop
    if ($running) {
        if (kill 15, $pid) {
            print "krang_schedulectl $command: waiting for krang_scheduled " . "to stop";
        } else {
            die "krang_schedulectl $command: krang_scheduled could not be " . "stopped\n";
        }

        # wait for stop - check pid file for removal
        my $stopped = 0;
        for my $wait (0 .. MAX_RESTART_WAIT) {
            if (not -e $pid_file) {
                $stopped = 1;
                last;
            }
            print ".";
            sleep 1;
        }

        if ($stopped) {
            print "\nkrang_schedulectl $command: krang_scheduled stopped\n";
        } else {
            die "\nkrang_schedulectl $command: krang_scheduled not stopped " . "in time\n";
        }

    }

    # start (Only if SchedulerMaxChildren is not 0)
    if (SchedulerMaxChildren > 0) {
        print "krang_schedulectl $command: starting krang_scheduled\n";

        setup_logs();

        unless (system($scheduled)) {
            print "krang_schedulectl $command: krang_scheduled started\n";
            exit;
        } else {
            die "krang_schedulectl $command: krang_scheduled could not be " . "started\n";
        }
    } else {
        print "krang_schedulectl $command: skipping -- SchedulerMaxChildren set to 0.\n";
        exit;
    }
}

usage();

sub usage {
    print <<END;
usage: krang_schedulectl (start|stop|restart|debug)

  start      - start the schedule daemon
  stop       - stop the schedule daemon
  restart    - restart the schedule daemon if running

END

    exit;
}

# make sure logs get created with the right ownership when running as root
sub setup_logs {

    # only worry if we're root
    return if $< != 0 or $> != 0;

    foreach my $log (qw(krang.log)) {
        my $file = catfile(KrangRoot, 'logs', $log);
        next if -e $file;

        # create the file and chown it to KrangUser:KrangGroup
        system("touch", $file);
        system("chown", KrangUser . ":" . KrangGroup, $file);
    }
}

