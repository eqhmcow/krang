#!/usr/bin/perl
use warnings;
use strict;
use Getopt::Long;
use Pod::Usage;

=pod

=head1 NAME

krang_floodfill - create fake Krang data the easy way

=head1 SYNOPSIS

  krang_floodfill --sites 5 --cats 10

=head1 DESCRIPTION

This script creates fake Krang data for use during testing.  It uses
the standard UNIX dict file in F</usr/dict/words> (or
F</usr/share/dict/words> if that's not available) to generate random
text.

Stories and media created will be checked in after being created.
Templates will be checked in and deployed.

=head1 OPTIONS

  --sites    - number of sites to create, defaults to 3

  --cats     - number of categories to create, defaults to 15

  --contribs - number of contributors to create, defaults to 50

  --media    - number of media objects to create, defaults to 25

  --stories  - number of stories to create, defaults to 50

  --covers   - number of cover stories to create, defaults to (--cats/2)
               (must be less than the number of categories)

  --templates- whether or not to create templates (one per element). 
               Defaults to 1.

  --users    - number of users to create, defaults to 50

  --undo_script - set to the name of a perl script to generate which will 
               undo the floodfill by deleting generated content.  Defaults
               to not create an undo script.

=cut

use File::Spec::Functions qw(catdir catfile splitdir rel2abs);
use FindBin qw($RealBin);
use Config;

BEGIN {
    # Find a KRANG_ROOT based on path to bin
    my @dir = splitdir($RealBin);
    $ENV{KRANG_ROOT} ||= catdir(@dir[0 .. $#dir - 1]);

    # use $KRANG_ROOT/lib for modules
    my $lib    = catdir($ENV{KRANG_ROOT}, "lib");
    $ENV{PERL5LIB} = $ENV{PERL5LIB} ? "$ENV{PERL5LIB}:${lib}" :
      "${lib}";
    unshift @INC, $lib, "$lib/".$Config{archname};

    # load Krang::Conf (don't need to load Krang here because we won't
    # be slinging elements)
    eval { require Krang::Conf };
    die <<"END" if $@;
######################################################################

Cannot load Krang.

Error message:

$@

######################################################################
END
}

use Krang::Script;
use Krang::Conf qw(KrangRoot SiteServerPort);
use Krang::ElementLibrary;
use Krang::Site;
use Krang::Category;
use Krang::Pref;
use Krang::User;
use Krang::Template;
use Krang::Story;
use Krang::Media;
use Krang::Group;
use Krang::Publisher;

use Time::Piece;
use Imager;

my ($help, $man);
my $sites    = 3;
my $cats     = 15;
my $contribs = 50;
my $stories  = 50;
my $covers;
my $media    = 25;
my $users    = 50;
my $templates = 1;
my $undo_script;
pod2usage(2) unless
  GetOptions(help         => \$help,
             man          => \$man,
             'sites=s'    => \$sites,
             'cats=s'     => \$cats,
             'contribs=s' => \$contribs,
             'stories=s'  => \$stories,
             'media=s'    => \$media,
	     'users=s'	  => \$users,
             'templates=s'=> \$templates,
             'covers=s'   => \$covers,
             'undo_script=s' => \$undo_script,
            );
$covers = defined $covers ? $covers : ($cats >= 2 ? int($cats / 2) : 0);

pod2usage(1)             if $help;
pod2usage(-verbose => 2) if $man;

# list of available sites, configured in developers /etc/hosts
my @site_urls = qw(
propositions.kra storeys.kra deallocations.kra wades.kra navigate.kra
cannon.kra tribe.kra allocates.kra precipitate.kra vanquishing.kra
vaticanizes.kra companies.kra expertness.kra crutch.kra fondles.kra
orangutan.kra equilibrium.kra mystics.kra chirped.kra bogartian.kra
backstitches.kra outset.kra certainly.kra possums.kra mercenariness.kra
stationing.kra menstruate.kra collects.kra superscripted.kra forgivably.kra
flo.kra checkerboarding.kra attainments.kra feud.kra spooler.kra shuttered.kra
slurs.kra beautifying.kra farkas.kra outperformed.kra subtree.kra tautness.kra
stella.kra renaults.kra synthesizers.kra toryizes.kra outlasts.kra limp.kra
collision.kra taoist.kra
);

# create sites
my (@sites, @cats);
for(1 .. $sites) {
    my $name = $site_urls[int(rand(scalar(@site_urls)))];
    my $site = Krang::Site->new(publish_path => 
                                  catdir(KrangRoot, "tmp", "${name}_publish"),
                                preview_path => 
                                  catdir(KrangRoot, "tmp", "${name}_preview"),
                                preview_url  => "preview.$name" .
                                  (SiteServerPort eq '80' ? 
                                   "" : ":" . SiteServerPort),
                                url          => "$name" .
                                  (SiteServerPort eq '80' ? 
                                   "" : ":" . SiteServerPort));
    eval { $site->save() };
    redo if $@ and $@ =~ /duplicate/;
    print STDERR "Created site " . $site->url . "...\n";
    push(@sites, $site);

    # remember root category
    my ($root) = Krang::Category->find(site_id => $site->site_id);
    push(@cats, $root);
}


# create categorie
for(1 .. $cats) {
    my $cname = get_word();
    my $cat = Krang::Category->new(dir       => $cname,
                                   parent_id => $cats[int(rand(scalar(@cats)))]->category_id);
    eval { $cat->save() };
    redo if $@ and $@ =~ /duplicate/;
    print STDERR "Created category " . $cat->url . "...\n";
    push(@cats, $cat);
}


# create contribs
my %types = Krang::Pref->get('contrib_type');
my @contribs;
for(1 .. $contribs) {
    my $fname = ucfirst(get_word());
    my $lname = ucfirst(get_word());
    my $mname = int(rand(2)) ? ucfirst(get_word()) : "";
    my $prefix = ("Sir", "General", "Her Royal Highness",
                  ("") x 20)[int(rand(23))];
    my $suffix = ("Jr.", "Sr.", "III", "IV",
                  ("") x 20)[int(rand(24))];
    my @types = grep { int(rand(2)) } keys %types;
    @types = (keys %types)[0] unless @types;

    my $contrib = Krang::Contrib->new(prefix => $prefix,
                                      first  => $fname,
                                      middle => $mname,
                                      last   => $lname,
                                      email  => "$fname\@$lname.com",
                                      phone  => "1-800-$lname",
                                      bio    => join(' ',
                                                     map { get_word() }
                                                         (0 .. 20)),
                                      url => "http://$fname.$lname.com");
    $contrib->contrib_type_ids(@types);
    eval { $contrib->save() };
    redo if $@ and $@ =~ /duplicate/;
    push(@contribs, $contrib);
    print STDERR "Created contributor " .
      join(' ', grep { $_ } ($prefix, $fname, $mname, $lname, $suffix)) .
        " (" . join(', ', map { $types{$_} } @types) . ")" .
           "\n";
}

# create media
my @media;
for my $media_count (1 .. $media) {
    # create a random image
    my ($x, $y);
    my $img = Imager->new(xsize => $x = (int(rand(300) + 50)),
                          ysize => $y = (int(rand(300) + 50)),
                          channels => 3,
                         );

    # fill with a random color
    $img->box(color => Imager::Color->new(map { int(rand(255)) } 1 .. 3),
              filled => 1);

    # draw some boxes and circles
    for (0 .. (int(rand(8)) + 2)) {
        if ((int(rand(2))) == 1) {
            $img->box(color =>
                      Imager::Color->new(map { int(rand(255)) } 1 .. 3),
                      xmin => (int(rand($x - ($x/2))) + 1),
                      ymin => (int(rand($y - ($y/2))) + 1),
                      xmax => (int(rand($x * 2)) + 1),
                      ymax => (int(rand($y * 2)) + 1),
                      filled => 1);
        } else {
            $img->circle(color =>
                         Imager::Color->new(map { int(rand(255)) } 1 .. 3),
                         r => (int(rand(100)) + 1),
                         x => (int(rand($x)) + 1),
                         'y' => (int(rand($y)) + 1));
        }
    }

    # pick a format
    my $format = (qw(jpg png gif))[int(rand(3))];

    $img->write(file => catfile(KrangRoot, "tmp", "tmp.$format"));
    my $fh = IO::File->new(catfile(KrangRoot, "tmp", "tmp.$format"))
      or die "Unable to open tmp/tmp.$format: $!";

    # Pick a type
    my %media_types = Krang::Pref->get('media_type');
    my @media_type_ids = keys(%media_types);
    my $media_type_id = $media_type_ids[int(rand(scalar(@media_type_ids)))];

    # create a media object
    my $media = Krang::Media->new(title      => join(" ",
                                                     map {ucfirst(get_word())}
                                                     (1..(int(rand(3)) + 2))),
                                  filename   => get_word() . ".$format",
                                  filehandle => $fh,
                                  category_id => $cats[int(rand(scalar(@cats)))]->category_id,
                                  media_type_id => $media_type_id,
                                  );
    eval { $media->save };
    if ($@) {
        if (ref($@) and ref($@) eq 'Krang::Media::DuplicateURL') {
            redo;
        } else {
            die $@;
        }
    }
    $media->checkin;
    push(@media, $media);
    unlink(catfile(KrangRoot, "tmp", "tmp.$format"));
    print STDERR "Created Media " . $media->url . "\n";
}


# create stories
my @stories;
for my $story_count (1 .. $stories) {

    # determine categories, up to 3
    my @c = ($cats[int(rand(scalar(@cats)))]);
    push(@c, $cats[int(rand(scalar(@cats)))])
      if (int(rand(2)));
    push(@c, $cats[int(rand(scalar(@cats)))])
      if (int(rand(2)));
    my %c = map { ($_->category_id,$_) } @c;
    @c = values(%c);

    # create story
    my $story;

    eval {
        $story = Krang::Story->new(title     => join(" ",
                                                     map {ucfirst(get_word())}
                                                     (1..(int(rand(3)) + 2))),
                                   slug       => get_word(),
                                   categories => \@c,
                                   cover_date => get_date(),
                                   class      => "article");
    };
    if ($@) {
        if (ref($@) and ref($@) eq 'Krang::Story::DuplicateURL') {
            redo;
        } else {
            die $@;
        }
    }

    # add some contribs, up to 5
    my @con;
    for(1 .. (int(rand(4)) + 1)) {
        my $contrib = $contribs[int(rand(scalar(@contribs)))];
        my @types = $contrib->contrib_type_ids;
        $contrib->selected_contrib_type($types[int(rand(scalar(@types)))]);
        push(@con, $contrib)
          unless grep { $_->contrib_id == $contrib->contrib_id } @con;
    }
    $story->contribs(@con);

    # make a multi-page story
    my $element = $story->element;
    my $deck = join(" ", map { get_word() } (1 .. ((int(rand(100)))+1)));
    $element->child('deck')->data($deck);
    $element->add_child(class => "page") for (1 .. ((int(rand(5)))+1));

    # foreach page, add some paragraphs and photos
    foreach my $page (grep { $_->name eq 'page' } $element->children()) {
        $page->child('header')->data(join(" ", map { ucfirst(get_word()) } (1 .. ((int(rand(6)))+1))));
        foreach my $obj_num (1 .. ((int(rand(20)))+1)) {
            if ((int(rand(3))) != 1) {
                my $para = join(" ",
                                map { get_word() } 
                                (0 .. ((int(rand(100)))+1)));
                $page->add_child(class => "paragraph",
                                 data => $para);
            } else {
                $page->add_child(class => "photo",
                                 data => $media[int(rand(scalar(@media)))]);
            }
        }
    }

    # add some keywords
    $element->child('fancy_keyword')->data([ map { get_word() } (1 .. ((int(rand(10)))+1))]);

    $story->save;
    $story->checkin;

    print STDERR "Created Story " . $story->url . "\n";
    push(@stories, $story);
}

# create covers
my @covers;
for my $cover_count (1 .. $covers) {
    my $c = $cats[$cover_count];

    # create story
    my $cover;

    $cover = Krang::Story->new(title     => join(" ",
                                                 map {ucfirst(get_word())}
                                                 (1..(int(rand(3)) + 2))),
                               slug       => get_word(),
                               categories => [$c],
                               cover_date => get_date(),
                               class      => "cover");

    # add some photos and lead-ins to stories
    my $element = $cover->element;
    foreach my $obj_num (1 .. ((int(rand(30)))+1)) {
        if ((int(rand(3))) != 1) {
            $element->add_child(class => "leadin",
                             data => $stories[int(rand(scalar(@stories)))]);
        } else {
            $element->add_child(class => "photo",
                             data => $media[int(rand(scalar(@media)))]);
        }
    }

    # setup a header
    $element->child('header')->data(join(' ', map { get_word() } 
                                         (0 .. ((int(rand(10)))+1))));

    $cover->save;
    $cover->checkin;

    print STDERR "Created Cover " . $cover->url . "\n";
    push(@covers, $cover);
}



# create templates
my @templates;
if ($templates) {
    my $publisher = Krang::Publisher->new();
    my @estack = map { Krang::ElementLibrary->top_level(name => $_) }
      Krang::ElementLibrary->top_levels;
    while (@estack) {
        my $element = pop(@estack);
        push(@estack, $element->children);
        
        my $bgcolor = "#" . 
          join('', map { (3 .. 9, 'A' .. 'F')[int(rand(13))] } (1 .. 6));
        
        # draw a labeled box for the element
        my $display_name = $element->display_name;    
        my $content = <<END;
<div style='border: 1px solid black; margin-left: 3px; margin-top: 10px; margin-right: 5px; padding-left: 3px; padding-right: 3px; padding-bottom: 3px; background-color: $bgcolor'>
  <span style='border: 1px; border-style: dashed; border-color: #AAA; padding-left: 5px; padding-right: 5px; background-color: white; text-color: whte; top: -7px; left: 5px; position: relative; width: 150px;'>$display_name</span><br>
END
        
        if ($element->children) {
            # container
            $content .= "<tmpl_loop element_loop>\n";
            foreach my $child ($element->children) {
                $content .= "<tmpl_if is_" . $child->name . ">".
                  "<tmpl_var name='" . $child->name . "'>".
                    "</tmpl_if>\n";
            }
            $content .= "</tmpl_loop>";
        } elsif ($element->isa('Krang::ElementClass::MediaLink')) {
            # media link
            $content .= "<img src='<tmpl_var name='url'>'>\n";
        } else {
            # data
            $content .= "<tmpl_var name='" . $element->name . "'>\n";
        }
        
        if ($element->name eq 'category') {
            $content .= Krang::Publisher->content;
        }
        
        $content .= "</div>";
        
        my $tmpl = Krang::Template->new(content		   => $content,
                                        element_class_name => $element->name);
        eval { $tmpl->save; };
        next if $@;
        
        push @templates, $tmpl;
        $publisher->deploy_template(template => $tmpl);
        $tmpl->checkin;
        print STDERR "Created Template: ", $tmpl->url, "\n";
    }
}


# create users
my %groups = map {$_->group_id, $_->name} Krang::Group->find();
my @users;
for(1 .. $users) {
    my $login = lc(get_word());
    my $fname = ucfirst(get_word());
    my $lname = ucfirst(get_word());
    my $password = lc(get_word());
    my @groups = grep { int(rand(5)) } keys %groups;
    @groups = (keys %groups)[0] unless @groups;

    my $user = Krang::User->new(login 		=> $login,
                                first_name  	=> $fname,
                                last_name   	=> $lname,
                                email  		=> "$fname\@$lname.com",
                                password	=> $password,
                                phone		=> "1-800-$lname",);
    $user->group_ids(@groups);
    eval { $user->save() };
    redo if $@ and $@ =~ /duplicate/;
    push(@users, $user);
    print STDERR "Created user " .
      join(' ', grep { $_ } ("$login:", $fname, $lname)) .
        " (" . join(', ', map { $groups{$_} } @groups) . ")" .
          "\n";
}

# create an undo script if requested
if ($undo_script) {
    my @undo;
    for my $rec ([ media => \@media ],
                 [ story => \@stories ],
                 [ story => \@covers ],
                 [ contrib => \@contribs ],
                 [ template => \@templates ],
                 [ category => [ reverse grep { $_->parent_id } @cats] ],
                 [ site => \@sites ],
                 [ user => \@users ]) {
        my ($name, $list) = @$rec;
        my $meth = "${name}_id";
        push(@undo, (map { 
            ("print STDERR qq{Deleting $name $_...\\n};",
             "(Krang::" . ucfirst($name) . 
             "->find($meth => $_))[0]->delete;") }
                 map { $_->$meth } @$list));
    }

    print STDERR "Writing undo script to '$undo_script'...\n";
    open(UNDO, '>', $undo_script) or die "Unable to open '$undo_script': $!";
    print UNDO <<UNDOEND;
#!/usr/bin/perl -w 
use strict;
BEGIN { \$ENV{KRANG_ROOT} = '$ENV{KRANG_ROOT}' }
use lib '$ENV{KRANG_ROOT}/lib';
use Krang::Script;
use Krang::Conf qw(KrangRoot);
use Krang::ElementLibrary;
use Krang::Site;
use Krang::Category;
use Krang::Pref;
use Krang::User;
use Krang::Template;
use Krang::Story;
use Krang::Media;

UNDOEND


    print UNDO join("\n", @undo), "\n";
    
    print UNDO "print qq{Deleting self.  Goodbye cruel world!\\n};\n";
    print UNDO "unlink \$0 or die qq{Unable to delete \$0 : \$!};\n";
    close UNDO;
    system("chmod +x $undo_script");
}

# get a random date, with time set to midnight
sub get_date {
    my $m = int rand(11) + 1;
    my $d = int rand(27) + 1;
    my $y = int rand(50) + 1975;
    return Time::Piece->strptime("$m/$d/$y", '%m/%d/%Y');
}


# get a random word
BEGIN {
    my @words;
    open(WORDS, "/usr/dict/words")
      or open(WORDS, "/usr/share/dict/words")
        or die "Can't open /usr/dict/words or /usr/share/dict/words: $!";
    while (<WORDS>) {
        chomp;
        push @words, $_;
    }
    srand (time ^ $$);

    sub get_word {
        return lc $words[int(rand(scalar(@words)))];
    }
}

# we outy
exit(0);
