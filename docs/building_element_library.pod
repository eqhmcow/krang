=head1 Creating an Element Library in Krang

This document is a guide to creating an element library in Krang.
Being able to build your own element library (or customize an existing
element library) is a very important concept in Krang, as it unlocks
most of the power and flexibility within Krang.

This guide is aimed developers with experience working in Perl (Krang
is a pure-Perl application).  Experience with Object-Oriented
development, especially in Perl, will be extremely useful.  At the
very least, be familiar with OO design methodologies.

Before reading this document, you should be comfortable with how Krang
works, and be able to set up a Krang installation for your own
development use.

Reccommended reading ahead of this document would be:

=over

=item *

HREF[Running Krang on Your Local Desktop|krang_on_desktop.html] - A
guide to setting up Krang on your local machine for development
purposes.

=item *

HREF[Element System|element_system.html] - An overview of the Krang
Element System.

=item *

HREF[Writing Templates|writing_htmltemplate.html] - A guide to
template creation in Krang.

=back


=head1 What an Element Set Looks Like

This guide will use the C<Default> element set that comes with Krang -
it can be found at C<KRANG_ROOT/element_lib/Default/>.  It will be
helpful if you configure a Krang instance (see 
HREF[Krang Configuration|configuration.html]) using the Default
element set as well, to follow along within the Krang web interface.

Borrowing from the
HREF[Krang Element System Overview|element_system.html], an Element
Set (or Library - the terms are interchangable) describes how all
story and category data is structured and handled in a Krang instance.

Every story within Krang has a number of standard fields - title,
slug, category, publish date.  Beyond that, everything is defined by
the Element set.

When a story is created, a Type has to be chosen - this chooses a root
element for the story, and determines what fields will be made
available to the user.

For example, if you create a story with a type of C<Article>, you will
immediately have the following fields presented in the web UI:

  - Metadata Title
  - Metadata Description
  - Metadata Keywords
  - Promo Title
  - Promo Teaser
  - Deck
  + Page


Open up the file C<KRANG_ROOT/element_lib/Default/article.pm>.  What
you will see in the subroutine C<new()> is a parameter C<children>
that defines all of those elements, plus some additional ones.  The
code begins with:

  sub new {
     my $pkg = shift;
     my %args = ( name => 'article',
                  children => [
                      Krang::ElementClass::Text->new( name => 'metadata_title',
                                                      display_name => 'Metadata Title',
                                                      min => 1,
                                                      max => 1,
                                                      reorderable => 0,
                                                      allow_delete => 0,
                                                    ),

This is where element relationships are set.  This defines the
C<article> element as having a series of children, each one with its
own settings as to its' appearance, its' name, how many of them exist,
etc.

These definitions are used by Krang as a guide during the process of
content manipulation - both in the web UI and in the Krang API, users
are limited to those actions that are deemed legal by the Element Set.
For example, looking at the code sample above, a user will not be
allowed to delete, re-order, or create additional C<metadata_title>
elements.

Other elements, however, will afford much greater flexibility to users.

=head3 A Note on Element Manipulation

One of the most important things to remember when working with element
libraries is this:

B<Maintain Compatibility with Existing Content>.

It is quite easy to make changes to your element library that will
break existing stories.  For example, if you were to remove the entry
for C<metadata_title> in the above code sample, all existing stories
of type C<Article> would immediately break, as they would all contain
an element that is no longer in the definition of an article.

If you are considering extensive changes to an element set currently
in use, you may need to write a script to migrate existing content.
This will be covered later.


=head1 Elements in Krang

Within Krang, an element is split into two parts, with separate APIs:

=over

=item 1

The element itself, covered by L<Krang::Element>.  The
L<Krang::Element> API covers general element behavior -
creation/deletion, data storage/retrieval, children, tree location,
etc.


=item 2

The second part is the element's class, covered
by L<Krang::ElementClass> and the modules derived from it
(e.g. L<Krang::ElementClass::TopLevel>,
L<Krang::ElementClass::Textarea>, L<Krang::ElementClass::StoryLink>).

=back

It is the definitions in L<Krang::ElementClass> and its subclasses
that determine how the element being created will behave - how it is
be presented to the user in the UI, what data it will store, how it
publishes itself, and so on.

Every element in an Element Set is a subclass of either
L<Krang::ElementClass> or one of its subclasses.

=head1 Setting up an Element Set

This section of the guide will walk through the creation of a very
basic element set.

=head2 Step 1 - The Element Set Spec

The most important thing when creating an element set is deciding how
all the elements will relate to eachother.  Once in use, making
changes to an element set becomes much more difficult (see L<Revisions
to a Live Element Set>).  Like all other development, spending the
extra time in the design stage will save you a lot of time down the
line.

The example element set used here has the following requirements:

=over

=item *

A multi-page story type (to be called C<simple_article>).

The story will have a single headline and deck common to all pages.
Each page in the story will have a page header, along with zero or
more of the following: paragraphs, links to images, and links to other
stories.


=item *

A cover story type (C<cover_story>).

A cover story is an index page for a given category.  It is a
single-page story, with a headline, and zero or more of the following:
paragraphs, links to images, and links to other stories.


=item *

A category element (C<category>).

The category will have a display_name, and nothing else at the moment.


=back

These requirements define a very basic element set - a real site would
have a far more intricate element set, but this will suffice for
educational purposes.

These requirements can now be broken out into element trees, which
would look something like this:

  Multi-Page Story:

  + basic_article
     - headline       (textbox)
     - deck           (textarea)
     + page
        - page_header (textbox)
        - paragraph   (textarea)
        - story_link  (storylink)
        - image_link  (medialink)



  Cover Story:

  + cover_story
     - headline    (textbox)
     - paragraph   (textarea)
     - story_link  (storylink)
     - image_link  (medialink)



  Category:

  + category
     - display_name  (textbox)


By putting together this tree, we now have a much clearer idea of
what's needed, how it's organized, and what modules need to be
developed.

We can see now take the tree above and turn it into a list of Krang
elements.

=over

=item *

C<basic_article> - a subclass of the L<Krang::ElementClass::TopLevel>
(more on that later) element, with the following children:

=over

=item *

C<headline> - a L<Krang::ElementClass::Text> element.

=item *

C<deck> - a L<Krang::ElementClass::Textarea> element.

=item *

C<page> - a subclass of L<Krang::ElementClass>.  C<page> stores no
data of its own, but it has the following children:

=over

=item *

C<page_header> - a L<Krang::ElementClass::Text> element.

=item *

C<paragraph> - a L<Krang::ElementClass::Textarea> element.

=item *

C<story_link> - a L<Krang::ElementClass::StoryLink> element.

=item *

C<image_link> - a L<Krang::ElementClass::MediaLink> element.

=back

=back

=item *

C<cover_story> - a subclass of the L<Krang::ElementClass::Cover>
element, with the following children:

=over

=item *

C<headline> - a L<Krang::ElementClass::Text> element.

=item *

C<paragraph> - a L<Krang::ElementClass::Textarea> element.

=item *

C<story_link> - a L<Krang::ElementClass::StoryLink> element.

=item *

C<image_link> - a L<Krang::ElementClass::MediaLink> element.

=back

=item *

C<category> - a subclass of the L<Krang::ElementClass::TopLevel>
element, with the following children:

=over

=item *

C<display_name> - a L<Krang::ElementClass::Text> element.

=back

=back


Anything that subclasses a Krang element will need to be written
(C<basic_article>, C<page>, C<cover_story>, C<category>).

Everything else has already been developed.  We simply need to list
the children properly in the elements we are developing.



=head2 Step 2 - Creating category.pm


The first step is to create a directory for the new element library.

We're going to call the new element set C<Tutorial>, so we create the
directory C<KRANG_ROOT/element_lib/Tutorial/>.

Now, we create C<KRANG_ROOT/element_lib/Tutorial/category.pm> -
it's the simplest of the three.

Every element set in Krang needs to have a category element.  Category
elements are used to build category-specific output (e.g. nav bars,
containers, etc), and as a result, are required.

This category element is quite simple - it has a display_name field,
something that a user can then populate, and have show up on output.
category.pm looks like this:


  package Tutorial::category;

  use strict;
  use warnings;

  =head1 NAME

  Tutorial::category

  =head1 DESCRIPTION

  category element class for Tutorial.  It has a display_name for a sub-element.

  =cut

  use base 'Krang::ElementClass::TopLevel';

  sub new {

      my $pkg  = shift;

      my %args = ( name     => 'category',
                   children => [
                                Krang::ElementClass::Text->new(name => 'display_name',
                                                               allow_delete => 0,
                                                               min => 1,
                                                               max => 1,
                                                               reorderable => 0,
                                                               required => 1),
                               ],
                   @_);
      return $pkg->SUPER::new(%args);
  }


What this does is the following:

=over

=item *

Define a C<Tutorial::category> object as being a subclass of
L<Krang::ElementClass::TopLevel>.

=item *

The default name for a C<Tutorial::category> element is 'category'.

=item *

A C<Tutorial::category> element can have a single child,
C<display_name>.  C<display_name> has the following properties:

=over

=item *

it will be added by default (C<< min => 1 >>).

=item *

no additional C<display_name> elements can be added (C<< max => 1 >>).

=item *

it cannot be deleted (C<< allow_delete => 0 >>).

=item *

If there were additional elements here, you could not change its
position in the order of elements (C<< reorderable => 0 >>).

=item *

The user is required to enter some data in it - it cannot be left
empty (C<< required => 1 >>).

=back

=back

There are many additional options that can be used at this point -
read the API documentation on L<Krang::ElementClass> to get a further
idea of what can be done.

B<NOTE>: The C<@_> at the end of the C<%args> definition - what this
does is allow you to add to or override any of the arguments that are
listed here at element instantiation.  For example,

  my $element = Tutorial::category->new(max => 5);

Would create a C<Tutorial::category> element object, with all the
parameters above, but direct Krang to not allow a user to create more
than 5 of them in the location where this one is created.

On the other hand,

  my $element = Tutorial::category->new(name => 'new_category');

Would change the name from 'category' (as defined originally) to
'new_category'.

=head2 Step 3 - Creating cover_story

Repeating the process we started with C<category>, we now create
C<KRANG_ROOT/element_lib/Tutorial/cover_story.pm>:

  package Tutorial::cover_story;

  use strict;
  use warnings;

  =head1 NAME

  Tutorial::cover_story

  =head1 DESCRIPTION

  cover_story element class for Tutorial.  It has the following sub-elements:

  headline, paragraph, story_link, image_link

  =cut

  use base 'Krang::ElementClass::Cover';

  sub new {

      my $pkg  = shift;

      my %args = ( name     => 'cover_story',
                   children => [
                                Krang::ElementClass::Text->new(name => 'headline',
                                                               allow_delete => 0,
                                                               size => 40,
                                                               min => 1,
                                                               max => 1,
                                                               reorderable => 0,
                                                               required => 1),

                                Krang::ElementClass::Textarea->new(name => 'paragraph'),

                                Krang::ElementClass::StoryLink->new(name => 'story_link'),

                                Krang::ElementClass::MediaLink->new(name => 'media_link'),

                               ],
                   @_);
      return $pkg->SUPER::new(%args);
  }


We have now created:

=over

=item *

A definition for C<Tutorial::cover_story>, which is a subclass of L<Krang::ElementClass::Cover>.

=item *

C<cover_story> has the following children:

=over

=item *

C<headline> is a 40-character text box (see L<Krang::ElementClass::Text>) that will
always be present.  It cannot be deleted or reordered.

=item *

C<paragraph> is a L<Krang::ElementClass::Textarea> object, and will
create a 30x4 text area.  It will not show up by default, but the user
is free to add as many of them as they want, and delete or reorder
them as they see fit.

=item *

C<story_link> is a L<Krang::ElementClass::StoryLink> object.  It is
used to handle links to other stories within Krang.  Like
C<paragraph>, it will not show up by default, but users are free to
add as many as they want, and add/delete/reorder them with impunity.

=item *

C<media_link> is a L<Krang::ElementClass::MediaLink> object.  Like
C<story_link>, C<media_link> is responsible for handling links to all
media objects in Krang.


=back

=back

=head2 Step 4 - Configuration Changes

With a category and cover page, we now have enough to look at things
in Krang.  It's time to make some configuration changes so Krang knows
to look for the new element library.

=over

=item C<set.conf>

A configuration file for the element library needs to be created - The
following should go into C<KRANG_ROOT/element_lib/Tutorial/set.conf>:

  Version 1.0
  TopLevels category cover_story

What this tells Krang is that there are currently two Top-Level
elements in the C<Tutorial> element set, C<category> and
C<cover_story> (we will get to C<basic_article> later).  Krang will
look for them at C<KRANG_ROOT/element_lib/Tutorial/category.pm> and
C<KRANG_ROOT/element_lib/Tutorial/cover_story.pm>.

=item C<krang.conf>

Now, edit C<KRANG_ROOT/conf/krang.conf> to create a new instance in
Krang using the C<Tutorial> element set.  At the end of your
C<krang.conf>, add the following:

  <Instance tutorial>

     # the UI display name for this instance
     InstanceDisplayName "Tutorial"

     # the virtual host users will use to access this instance
     InstanceHostName cms.mytutorial.com

     # MySQL database name for this instance
     InstanceDBName tutorial

     # the element set to be used in this instance.  Instances may share
     # element sets.
     InstanceElementSet Tutorial

  </Instance>

=back

Now, run:

  $ bin/krang_createdb --all

HREF[krang_createdb|script_krang_createdb.html] will now iterate over
all configured Instances in C<conf/krang.conf>, and create databases
as needed (e.g. new Instances only).

At this point, you can re-start Krang.

  $ sudo bin/krang_ctl stop
  $ sudo bin/krang_ctl start


=head1 Checking the Results

When you log into the Tutorial instance, the first thing you will need
to do is HREF[create a site|krang_on_desktop.html#configuring a site]
for everthing to be published under.

=head2 The Cover Story Element

With the newly-created site, create a new story.  You will see only
one option available under the C<Type> pulldown - the C<Cover Story>
type we just created!

As you go through the process of story creation, you'll see that all
the sub-elements we defined are there:

=over

=item *

By default, a single C<Headline> that you cannot delete, and cannot
reorder.

=item *

Under the C<Add Element> section, the C<Paragraph>, C<Story Link> and
C<Media Link> elements, which have no limits as to additions,
deletetions, and reorderings.

=back

=head2 The Category Element

To look at (and edit) the category element, go to the B<Categories>
menu (in the left nav, in the B<Admin> section).

When you create a site in Krang, a category is automatically created,
representing the root of the site.

Edit that category, and you will see the single C<Display Name>
element that we added to the category definition.

The category UI works in the same fashion as the story UI.  If we had
made the category definition more complex (optional elements, etc),
you would have the same options to add/delete/reorder that you have
available to you in the story UI.


Congratulations, you now have completed story and category types!  The
one thing missing for both of these is output templates, which we will
address later.

=head1 Step 5 - Creating the basic_article

The basic_article story type is a little more involved.  Remember, the
structure of basic_article was defined earlier as a multi-page story:

  + basic_article
     - headline       (textbox)
     - deck           (textarea)
     + page
        - page_header (textbox)
        - paragraph   (textarea)
        - story_link  (storylink)
        - image_link  (medialink)


C<basic_article> has three children: C<headline>, C<deck>, and
C<page>.

But now C<page> has its own set of children: C<page_header>,
C<paragraph>, C<story_link> and C<image_link>.

So we will need to create two new files.  We will start with C<page>.

C<KRANG_ROOT/element_lib/Tutorial/page.pm>:

  package Tutorial::page;

  use strict;
  use warnings;

  =head1 NAME

  Tutorial::page

  =head1 DESCRIPTION

  the page element class for Tutorial.

  It will be used by basic_story - the multi-page story type.

  page has the following children:

  page_header, paragraph, story_link, image_link

  =cut

  use base 'Krang::ElementClass';

  sub new {
      my $pkg  = shift;
      my %args = ( name => 'page',
                   min  => 1,
                   pageable => 1,
                   children => [
                                Krang::ElementClass::Text->new(name => 'page_header',
                                                               min  => 1,
                                                               max  => 1,
                                                               reorderable  => 0,
                                                               allow_delete => 0
                                                              ),
                                Krang::ElementClass::Textarea->new(name => 'paragraph',
                                                                   min  => 1
                                                                  ),

                                Krang::ElementClass::StoryLink->new(name => 'story_link'),
                                Krang::ElementClass::MediaLink->new(name => 'image_link')
                               ],
                   @_
                 );

      return $pkg->SUPER::new(%args);

  }


The C<page> element cannot stand up by itself as a story type.  It is
designed to be a child of another element - C<basic_article>.

C<KRANG_ROOT/element_lib/Tutorial/basic_article.pm>:

  package Tutorial::basic_article;

  use strict;
  use warnings;

  =head1 NAME

  Tutorial::basic_article - simple article type for the Tutorial element
  library

  =head1 DESCRIPTION

  basic_article is a simple multi-page story type in the Tutorial
  element library.

  It has the following children: headline, deck, page (Tutorial::page).

  =cut

  use base 'Krang::ElementClass::TopLevel';

  sub new {
      my $pkg  = shift;
      my %args = (
                  name => 'basic_article',
                  children => [
                               Krang::ElementClass::Text->new(name => 'headline',
                                                              allow_delete => 0,
                                                              size => 40,
                                                              min => 1,
                                                              max => 1,
                                                              reorderable => 0,
                                                              required => 1
                                                             ),
                               Krang::ElementClass::Textarea->new(name         => 'deck',
                                                                  allow_delete => 0,
                                                                  reorderable  => 0,
                                                                  required     => 1,
                                                                  min => 1,
                                                                  max => 1
                                                                 ),
                               Tutorial::page->new(name => 'article_page',
                                                   min  => 1
                                                  )

                              ],
                  @_
                 );


      return $pkg->SUPER::new(%args);

  }

A few notes on C<basic_article>:

=over

=item *

C<basic_article> subclasses L<Krang::ElementClass::TopLevel> rather
than L<Krang::ElementClass::Cover>.

The reason is that the story slug (entered when creating a new story)
is used along with the story's category in building the URL for the
story.  Cover stories (those that subclass
L<Krang::ElementClass::Cover>) do not use the slug - they use only the
category path for the URL.

=item *

The C<< min => 1 >> argument in the lines:

 Tutorial::Page->new(name => 'article_page',
                     min  => 1)

is actually redundent - it was already made in the C<page.pm> file.
However, you could make the page element entirely optional by setting
it to 0 in this call.  Conversely, you could force every article to
contain multiple pages by setting C<min> to a value greater than 1.


=back


Finally, add C<basic_article> to C<set.conf>,

C<KRANG_ROOT/element_lib/Tutorial/set.conf>:

  Version 1.0
  TopLevels category cover_story basic_article

and restart Krang.

  $ sudo bin/krang_ctl stop
  $ sudo bin/krang_ctl start

Congratulations!  You now have the three most fundamental story
components of Krang.

=head1 Templates

Now that you have story types created, you need output templates.
Without output templates, you have no way of publishing your results.
In fact, try to preview one of your stories - Krang will complain.

Template design and construction is beyond the scope of this document
- that's all covered in
HREF[Writing HTML::Template Templates in Krang|writing_htmltemplate.html].

One important thing to note, however:

Templates are stored in the Krang database, not on the filesystem.
It's a B<VERY> good idea to create a .kds (Krang Data Set) file of
your templates to bundle with your element library.

Once you are finished with your template development, export the
templates as a .kds file:

  bin/krang_export --templates --output templates.kds

  cp templates.kds element_lib/Tutorial/templates.kds

An additional benefit:

If you create additional Krang instances using your element library,
C<bin/krang_createdb> will see the
C<element_lib/Tutorial/templates.kds> file, and automatically install
the templates for you.

Once finished with development of your element library
(e.g. everything works as planned, templates are finished, etc), you
may want to creating a Krang add-on out of your element library.
There are a number of benefits to this:

=over

=item *

Addons are an easy way to move element libraries from one Krang
installation to another.

=item *

You can maintain an addon separately from Krang.  Using a versioning
system like CVS/Subversion/Bitkeeper is now easy, and encouraged -
especially since Krang uses version numbers when tracking Addons.

=item *

Good general-purpose addons can be contributed to the general Krang
project, for the world at large (hint, hint!)

=back

Read HREF[Building a Krang Add-on|add_on.html] for more information on
creating an Add-on, and take a look at available
HREF[Krang Addons|http://krang.sourceforge.net/addons.html] to see
some existing addons.


=head1 Further Development

This section covers more advanced topics in element library development.

=head2 Lists

Lists in Krang are a very powerful concept - it allows you to provide
your editorial staff with pulldown menus in the editorial user
interface, pulldows whose content they can control themselves through
the C<Lists> interface.

The flexibility that lists provide is attractive to editorial staffs,
as they give nearly the flexibility of a textbox (over a pulldown
element whose options are fixed by the element definition), with none
of the potential for error through misspelling.

Additionally, Krang lists can be multidimensional and hierarchtical -
think of 2 lists, where the content of the second list is dependent on
the selection made in the first one.

The classic example is the Year/Make/Model search seen on a lot of car
websites - a manufacturer might have produced a 6000 SUX in 1999, but
not in 2000.

B<NOTE>: multidimensional lists are not supported properly in the
Krang UI in versions prior to 1.102.


=head3 Setting up Lists - lists.conf

The structure of the lists associated with an element set can be saved
in a config file in the same directory as the element set itself.

The F<lists.conf> file is an XML file containing a description of each
list group.  For example:

  <list_groups>

    <list_group list_group_name="Segments" description="This list group just contains one list">
      <list list_name="Segments" />
    </list_group>

    <list_group list_group_name="Cars" description="Year/Make/Model Car list">
      <list list_name="Year" />
      <list list_name="Make" />
      <list list_name="Model" />
    </list_group>

  </list_groups>

This config file describes two different lists:

=over

=item * 

the C<Segments> list is a one-dimensional list, that will show up as a single pulldown.

=item *

The C<Cars> list is a three-dimensional list, where the contents of
each pulldown will be dependent on the item chosen in the list above
it.


=item B<NOTE:>

The C<lists.conf> file is only read automatically when a
database is set up using C<bin/krang_createdb>.

If your database is already set up, you can use
C<bin/krang_create_lists> to create the lists in Krang specified by
your C<lists.conf> file.

=back

=head3 Using Lists in the Element System

To demonstrate how lists are used, we're going to build a new story type: C<pulldown_article>

  + pulldown_article
     - headline       (textbox)
     - deck           (textarea)
     - segments_list  (listgroup)
     - cars_list      (listgroup)
     + page
        - page_header (textbox)
        - paragraph   (textarea)
        - story_link  (storylink)
        - image_link  (medialink)

For the sake of simplicity, C<pulldown_article> is derived from
C<basic_article> - in fact, it's just C<basic_article> with the two
lists we created in C<lists.conf> added.

C<pulldown_article> looks as follows:


  package Tutorial::pulldown_article;

  use strict;
  use warnings;

  =head1 NAME

  Tutorial::pulldown_article - article type that makes use of Krang lists.


  =head1 DESCRIPTION

  Tutorial::pulldown_article is used to demonstrate how lists work in Krang.

  =cut

  use base 'Krang::ElementClass::TopLevel';

  sub new {
      my $pkg  = shift;
      my %args = (
                  name => 'pulldown_article',
                  children => [
                               Krang::ElementClass::Text->new(name => 'headline',
                                                              allow_delete => 0,
                                                              size => 40,
                                                              min => 1,
                                                              max => 1,
                                                              reorderable => 0,
                                                              required => 1
                                                             ),

                               Krang::ElementClass::Textarea->new(name         => 'deck',
                                                                  allow_delete => 0,
                                                                  reorderable  => 0,
                                                                  required     => 1,
                                                                  min => 1,
                                                                  max => 1
                                                                 ),

                               Krang::ElementClass::ListGroup->new(name       => 'segments',
                                                                   list_group => 'Segments',
                                                                   multiple   => 1,
                                                                   min => 1,
                                                                   max => 1,
                                                                  ),

                               Krang::ElementClass::ListGroup->new(name       => 'car_selector',
                                                                   list_group => 'Cars',
                                                                   multiple   => 0,
                                                                   min => 1,
                                                                   max => 1,
                                                                  ),

                               Tutorial::page->new(name => 'article_page',
                                                   min  => 1
                                                  )

                              ],
                  @_
                 );

      return $pkg->SUPER::new(%args);

  }


This should look familiar by now - the only addition over the
C<basic_article> story type are two L<Krang::ElementClass::ListGroup>
elements.  The first one uses the C<Segments> list we defined in
C<lists.conf>, the second uses the C<Cars> list.

The only parameter of note is the C<multiple> parameter - in
single-dimensional lists, you can allow the user to select multiple
options.  This will not work in multi-dimensional lists.

=head3 Using Lists in the User Interface

Once everything has been set up, the content of these lists needs to
be populated.  This can be done within the F<Lists> interface under
the Admin menu.

If you already have a large set of list information that needs to be
imported, this can be done programatically by using the API provided
by L<Krang::ListGroup>, L<Krang::List>, and L<Krang::ListItem>.

Once your lists are populated, simply create a new C<pulldown_article>
story, and you're good to go.


=head1 Additional Reading

This document only covers the basics in Krang Element Library
development.  Further reading:

=over

=item *

HREF[Customizing the Publish Process in Krang|customizing_publish.html] -
How to alter the Krang publish process to fit your needs.

=item *

L<Krang::ElementClass> - the base class for all Krang Elements

=item *

L<Krang::ElementClass::TopLevel> - the base class for top-level
element classes.

=item *

L<Krang::ElementClass::ListGroup> - The element class that handles
Krang lists.

=item *

L<Krang::ListGroup> - Groups one or more list (L<Krang::List>) objects.

=item *

L<Krang::List> - A list of items (L<Krang::ListItem> objects) to be
selected by a user within a story/category edit UI.

=item *

L<Krang::ListItem> - An item in a L<Krang::List> list.

=back
