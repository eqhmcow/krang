#!/usr/bin/perl
use warnings;
use strict;
use Getopt::Long;
use Pod::Usage;
use File::Spec::Functions qw(catdir catfile splitdir rel2abs canonpath);
use FindBin qw($RealBin);
use Config;

=pod

=head1 NAME

krang_uninstall - delete a krang install

=head1 SYNOPSIS

  krang_uninstall 

=head1 DESCRIPTION

This script will uninstall a krang instance by means
of database and file removal.

This script must be run as root.

Please use this script with utmost caution!

=head1 OPTIONS
  
    --verbose     - print a running dialogue of what happens, otherwise
                  actions are performed silently.

    --no_prompt   - suppress warning prompt

=cut

BEGIN {
    # Find a KRANG_ROOT based on path to bin
    my @dir = splitdir(canonpath($RealBin));
    $ENV{KRANG_ROOT} ||= catdir(@dir[0 .. $#dir - 1]);

    # use $KRANG_ROOT/lib for modules
    my $lib    = catdir($ENV{KRANG_ROOT}, "lib");
    $ENV{PERL5LIB} = $ENV{PERL5LIB} ? "$ENV{PERL5LIB}:${lib}" :
      "${lib}";
    unshift @INC, $lib, "$lib/".$Config{archname};

    # load Krang::Conf (don't need to load Krang here because we won't
    # be slinging elements)
    eval { require Krang::Conf };
    warn << "END" and exit(1) if $@;
######################################################################

Cannot load Krang.

Error message:

$@

######################################################################
END
}

use Krang::ClassFactory qw(pkg);
use Term::Prompt qw(prompt);
use Krang::ClassLoader Conf => qw(KrangRoot HostName InstanceDBName DBUser DBPass DBHost);

my ($help, $man, $no_prompt, $MySQLRootPassword);
my $verbose;

pod2usage(2) unless
  GetOptions(help         => \$help,
             man          => \$man,
             verbose => \$verbose,
             no_prompt => \$no_prompt,
             'MySQLRootPassword=s' => \$MySQLRootPassword
            );

pod2usage(1)             if $help;
pod2usage(-verbose => 2) if $man;

unless ($no_prompt) {
    exit 1
      unless prompt("y", "This program will totally and permanently remove this krang installation.\n\nAre you sure you
want to proceed?", "y/n", "y");
}

# Must be running as root.
exit_error("Must be run as root.") unless ($> eq "0");

# stop krang services
system($ENV{KRANG_ROOT}."/bin/krang_ctl stop");

# drop all databases
my $cmd = $ENV{KRANG_ROOT} . "/bin/krang_createdb" .
          " --destroy --all --no_create --no_prompt";
$cmd .= " --verbose --verbose" if $verbose;
$cmd .= " --hostname=" . DBHost if DBHost;
$cmd .= " --password=" . $MySQLRootPassword if $MySQLRootPassword;

system($cmd) == 0
  or die "Unable to remove existing database: $?";

# now blow all files away!
print "\nNow removing all files...\n\n" if $verbose;
system("rm -Rf $ENV{KRANG_ROOT}");

# it worked.
exit(0);

sub exit_error {
    my $cmd = $0;
    $cmd =~ s/.*\/(.+)$/$1/;

    my $message = shift;
    print STDERR "\nError: $cmd:  $message\n\n";
    exit(1);
}
